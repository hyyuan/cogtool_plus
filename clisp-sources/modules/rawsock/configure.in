dnl AUTOCONF configuration for RAWSOCK
dnl Sam Steingold 2004-2005

AC_PREREQ(2.57)
AC_INIT(rawsock, 1.0, clisp-list)
AC_CONFIG_SRCDIR(sock.lisp)
AC_CONFIG_HEADERS(config.h)

AC_MSG_NOTICE([* RAWSOCK (Tools)])
AC_PROG_CC
AC_PROG_CPP

AC_MSG_NOTICE([* RAWSOCK (Headers)])
AC_HEADER_STDC
AC_HEADER_TIME
CL_TCPCONN
AC_CHECK_HEADERS([sys/socket.h linux/if_packet.h netdb.h sys/uio.h \
sys/types.h sys/ioctl.h errno.h stropts.h poll.h sys/un.h])
AC_CHECK_HEADERS([net/if.h],[],[],[
#if HAVE_SYS_SOCKET_H
# include <sys/socket.h>
#endif
])
AC_CHECK_HEADERS([netinet/if_ether.h],[],[],[
#if HAVE_SYS_SOCKET_H
# include <sys/socket.h>
#endif
#if HAVE_NET_IF_H
# include <net/if.h>
#endif
])
if test "$ac_cv_header_sys_socket_h" = "no"; then
AC_CHECK_HEADERS([winsock2.h ws2tcpip.h])dnl windows
else AC_CHECK_MEMBERS([struct msghdr.msg_flags, struct msghdr.msg_control],,,
[#include <sys/socket.h>])
fi
AC_CHECK_SIZEOF(ssize_t)
AC_CHECK_SIZEOF(size_t)
AC_CHECK_TYPES(struct iovec,,,[
#if defined(HAVE_SYS_TYPES_H)
#include <sys/types.h> /* for size_t */
#endif
#if defined(HAVE_SYS_UIO_H)
#include <sys/uio.h>
#endif
])

AC_MSG_NOTICE([* RAWSOCK (Functions)])
if test "$ac_cv_header_winsock2_h" = "yes"; then
  dnl this is windows, we know that socket() is in ws2_32
  dnl note that AC_SEARCH_LIBS will not work because linking with ws2_32
  dnl requires including <winsock2.h>
  AC_MSG_CHECKING([for socket() in ws2_32])
  LIBS=${LIBS}' -lws2_32';
  AC_LINK_IFELSE(AC_LANG_PROGRAM([#include <winsock2.h>],[socket(0,0,0)]),
  [ac_cv_search_socket=yes
  AC_MSG_RESULT(yes)],
  [ac_cv_search_socket=no
  AC_MSG_RESULT(no)])
else
  CL_SOCKET
fi
if test "$ac_cv_search_socket" = "no"; then
  AC_MSG_FAILURE([cannot find SOCKET library])
fi
AC_CHECK_FUNCS(socketpair sockatmark recvmsg sendmsg htonl htons ntohl ntohs dnl
readv writev getnameinfo getaddrinfo freeaddrinfo dnl
endprotoent getprotobyname getprotobynumber getprotoent setprotoent dnl
endnetent getnetbyaddr getnetbyname getnetent setnetent)
dnl AC_CHECK_FUNCS(inet_pton inet_ntop inet_addr inet_ntoa)
dnl CL_POLL
dnl CL_SELECT

AC_MSG_NOTICE([* RAWSOCK (Output)])
AC_CONFIG_FILES(Makefile link.sh)
AC_OUTPUT

AC_MSG_NOTICE([* RAWSOCK (Done)])
